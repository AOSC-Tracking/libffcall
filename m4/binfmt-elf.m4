dnl -*- Autoconf -*-
dnl Copyright (C) 2016 Free Software Foundation, Inc.
dnl This file is free software, distributed under the terms of the GNU
dnl General Public License.  As a special exception to the GNU General
dnl Public License, this file may be distributed as part of a program
dnl that contains a configuration script generated by Autoconf, under
dnl the same distribution terms as the rest of that program.

dnl From Bruno Haible.

AC_PREREQ([2.57])

dnl Provides a configure option --disable-elf-hack.
AC_DEFUN([FFCALL_BINFMT_ELF_OPTION],
[AC_ARG_ENABLE([elf-hack],
   [AS_HELP_STRING([--disable-elf-hack],
      [disable the hack for supporting shared libraries on ELF platforms])],
   [case $enableval in
      no) use_elf_hack=false ;;
      *) use_elf_hack=true ;;
    esac
   ])
])

dnl Tests whether the binary format is ELF.
dnl Sets BINFMT_ELF to true or false accordingly.
dnl Defines BINFMT_ELF as 1 or 0 accordingly.
AC_DEFUN([FFCALL_BINFMT_ELF],
[AC_REQUIRE([FFCALL_BINFMT_ELF_OPTION])
if $use_elf_hack; then
AC_CACHE_CHECK([whether the binary format is ELF], [ffcall_cv_binfmt_elf], [
AC_EGREP_CPP([Thranduil],
[#if defined __ELF__
Thranduil
#endif
], [ffcall_cv_binfmt_elf=yes], [ffcall_cv_binfmt_elf=no])])
ffcall_binfmt_elf=$ffcall_cv_binfmt_elf
else
ffcall_binfmt_elf=no
fi
if test $ffcall_binfmt_elf = yes; then
  BINFMT_ELF=true
  ffcall_binfmt_elf_value=1
else
  BINFMT_ELF=false
  ffcall_binfmt_elf_value=0
fi
AC_SUBST([BINFMT_ELF])dnl
AC_DEFINE_UNQUOTED([BINFMT_ELF],[$ffcall_binfmt_elf_value],[the binary format is ELF])
])
